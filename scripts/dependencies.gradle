ext {
    def threeTenBpVersion = "1.3.6"
    def androidArchVersion = "1.1.0"
    def mockWebServerVersion = "3.9.1"
    supportLibVersion = "27.0.2"
    rxJavaVersion = "2.1.9"

    libs = [
            kotlinStdLib         : "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion",

            appCompat            : "com.android.support:appcompat-v7:$supportLibVersion",
            design               : "com.android.support:design:$supportLibVersion",
            cardView             : "com.android.support:cardview-v7:$supportLibVersion",
            recyclerView         : "com.android.support:recyclerview-v7:$supportLibVersion",
            constraintLayout     : 'com.android.support.constraint:constraint-layout:1.1.0-beta4',

            androidArchExtensions: "android.arch.lifecycle:extensions:$androidArchVersion",
            androidArchCompiler  : "android.arch.lifecycle:compiler:$androidArchVersion",

            okHttp               : "com.squareup.okhttp3:okhttp:$mockWebServerVersion",
            okHttpLogging        : "com.squareup.okhttp3:logging-interceptor:$mockWebServerVersion",

            rxJava               : "io.reactivex.rxjava2:rxjava:$rxJavaVersion",
            rxAndroid            : 'io.reactivex.rxjava2:rxandroid:2.0.1',
            jsr305               : 'com.google.code.findbugs:jsr305:3.0.0',

            rxBinding            : "com.jakewharton.rxbinding2:rxbinding:2.0.0",
            glide                : "com.github.bumptech.glide:glide:4.6.1",

            threeTenAbp          : 'com.jakewharton.threetenabp:threetenabp:1.0.5',
            threeTenBp           : "org.threeten:threetenbp:$threeTenBpVersion",
            threeTenBpNoTzDb     : "org.threeten:threetenbp:$threeTenBpVersion:no-tzdb",

            junit                : "junit:junit:4.12",
            mockito              : "org.mockito:mockito-core:2.12.0",
            assertJ              : "org.assertj:assertj-core:2.9.0",
            mockWebServer        : "com.squareup.okhttp3:mockwebserver:$mockWebServerVersion",
            supportTestRunner    : "com.android.support.test:runner:1.0.1",
    ]

    //end region

    projectModules = [
            app               : project("app"),
            inMemoryRepository: project("inMemoryRepository"),
            okHttpGateway     : project("okHttpGateway"),
            domain            : project("domain"),
    ]

    appModuleDependencies = [
            [configuration: "implementation", dependency: projectModules.domain],
            [configuration: "implementation", dependency: projectModules.inMemoryRepository],
            [configuration: "implementation", dependency: projectModules.okHttpGateway],

            [configuration: "implementation", dependency: libs.appCompat],
            [configuration: "implementation", dependency: libs.design],
            [configuration: "implementation", dependency: libs.cardView],
            [configuration: "implementation", dependency: libs.recyclerView],
            [configuration: "implementation", dependency: libs.constraintLayout],

            [configuration: "implementation", dependency: libs.androidArchExtensions],

            [configuration: "implementation", dependency: libs.rxAndroid],
            [configuration: "implementation", dependency: libs.rxBinding],
            [configuration: "implementation", dependency: libs.threeTenAbp],

            [configuration: "implementation", dependency: libs.glide],
    ]

    domainModuleDependencies = [
            [configuration: "compile", dependency: libs.kotlinStdLib],
            [configuration: "compile", dependency: libs.rxJava],
            [configuration: "compile", dependency: libs.threeTenBpNoTzDb],
            [configuration: "compile", dependency: libs.jsr305]
    ]

    inMemoryRepositoryModuleDependencies = [
            [configuration: "implementation", dependency: projectModules.domain],
    ]

    okHttpGatewayModuleDependencies = [
            [configuration: "implementation", dependency: projectModules.domain],
            [configuration: "implementation", dependency: libs.okHttp],

            [configuration: "androidTestImplementation", dependency: libs.supportTestRunner],
            [configuration: "androidTestImplementation", dependency: libs.junit],
            [configuration: "androidTestImplementation", dependency: libs.assertJ],
            [configuration: "androidTestImplementation", dependency: libs.mockWebServer],
    ]
}

// Add relevant repositories
allprojects {
    repositories {
        google()
        jcenter()
    }
}

// Unify common libraries across all modules
allprojects {
    configurations.all {
        resolutionStrategy {
            eachDependency { DependencyResolveDetails details ->
                if (details.requested.group == 'com.android.support') {
                    details.useVersion supportLibVersion
                }
            }
            force "io.reactivex.rxjava2:rxjava:$rxJavaVersion"
        }
    }
}
